plugins {
    id 'war'
    id 'eclipse-wtp'
    id 'jvm-test-suite'
}

repositories {
    mavenCentral()
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

dependencies {
	providedCompile platform(group: 'jakarta.platform', name: 'jakarta.jakartaee-bom', version: '9.0.0')
    providedCompile platform(group: "org.eclipse.microprofile", name: "microprofile", version: "5.0")
    providedCompile group: "org.eclipse.microprofile", name: "microprofile"
    providedCompile group: 'jakarta.persistence', name: 'jakarta.persistence-api'
    providedCompile group: 'jakarta.transaction', name: 'jakarta.transaction-api'
    
    implementation platform(group: 'org.springframework.data', name: 'spring-data-bom', version: '2023.0.0')
    implementation group: 'org.springframework.data', name: 'spring-data-jpa'
	
	runtimeOnly group: 'com.h2database', name: 'h2', version: '2.1.212'
}

testing.suites {
    test {
        useJUnitJupiter('5.9.3')
    }
    integrationTest(JvmTestSuite) {
    	targets {
            all {
                testTask.configure { 
                	// configure server used for integration tests
			    	systemProperty "tomee.classifier", "plus"
			    	systemProperty "tomee.properties", "tomee.mp.scan=all"
                }
            }
        }
    	
    	dependencies {
    		implementation project()
    		implementation 'org.jboss.arquillian.junit5:arquillian-junit5-container:1.7.0.Final'
    		implementation 'org.glassfish.jersey.core:jersey-client:3.1.2'
    		runtimeOnly 'org.apache.tomee:arquillian-tomee-remote:9.0.0'
    		runtimeOnly 'org.apache.tomee:apache-tomee:9.0.0'
    	}
		configurations.integrationTestImplementation {
			extendsFrom configurations.testImplementation
		}
    }
}

eclipse.wtp.facet { facet name: 'jst.web', version: '5.0' }
